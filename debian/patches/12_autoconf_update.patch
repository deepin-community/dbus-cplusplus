Description: Update configure.ac and m4/acx_pthread.m4
Author: Thomas Uhle <thomas.uhle@mailbox.tu-dresden.de>
Forwarded: not-needed
Last-Update: 2023-02-08

--- a/configure.ac
+++ b/configure.ac
@@ -1,10 +1,10 @@
 # Autojunk script for libdbus-c++
 
-AC_PREREQ(2.59)
-AC_INIT([libdbus-c++], 0.9.0, [andreas.volz@tux-style.com])
+AC_PREREQ([2.69])
+AC_INIT([libdbus-c++], [0.9.0], [andreas.volz@tux-style.com])
 
-AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)
-AM_CONFIG_HEADER([config.h])
+AM_INIT_AUTOMAKE
+AC_CONFIG_HEADERS([config.h])
 AC_CONFIG_MACRO_DIR([m4])
 
 AC_CANONICAL_HOST
@@ -59,7 +59,7 @@ AC_SUBST(RT_LIBS)
 
 # Check for programs
 
-AC_LANG_CPLUSPLUS
+AC_LANG([C++])
 
 AC_PROG_CC
 AC_PROG_CXX
@@ -67,7 +67,7 @@ AC_PROG_CXX
 CXX_FOR_BUILD=${CXX_FOR_BUILD-${CXX}}
 AC_SUBST(CXX_FOR_BUILD)
 
-AM_PROG_LIBTOOL
+LT_INIT
 
 PKG_PROG_PKG_CONFIG
 
@@ -212,7 +212,7 @@ AC_SUBST(BUILD_LIBDBUS_CXX_DIR)
 
 # Save processed files
 
-AC_OUTPUT(
+AC_CONFIG_FILES([
 	Makefile
         src/Makefile
         src/integration/Makefile
@@ -240,7 +240,8 @@ AC_OUTPUT(
 	dbus-c++-glib-1.pc
 	dbus-c++-glib-1-uninstalled.pc
 	libdbus-c++.spec
-)
+])
+AC_OUTPUT
 
 echo
 echo "Build configuration:"
--- a/m4/acx_pthread.m4
+++ b/m4/acx_pthread.m4
@@ -82,7 +82,7 @@
 AC_DEFUN([ACX_PTHREAD], [
 AC_REQUIRE([AC_CANONICAL_HOST])
 AC_LANG_SAVE
-AC_LANG_C
+AC_LANG([C])
 acx_pthread_ok=no
 
 # We used to check for pthread.h first, but this fails if pthread.h
@@ -98,7 +98,7 @@ if test x"$PTHREAD_LIBS$PTHREAD_CFLAGS" != x; then
         save_LIBS="$LIBS"
         LIBS="$PTHREAD_LIBS $LIBS"
         AC_MSG_CHECKING([for pthread_join in LIBS=$PTHREAD_LIBS with CFLAGS=$PTHREAD_CFLAGS])
-        AC_TRY_LINK_FUNC(pthread_join, acx_pthread_ok=yes)
+        AC_LINK_IFELSE([AC_LANG_CALL([], [pthread_join])], [acx_pthread_ok=yes])
         AC_MSG_RESULT($acx_pthread_ok)
         if test x"$acx_pthread_ok" = xno; then
                 PTHREAD_LIBS=""
@@ -195,11 +195,12 @@ for flag in $acx_pthread_flags; do
         # pthread_cleanup_push because it is one of the few pthread
         # functions on Solaris that doesn't have a non-functional libc stub.
         # We try pthread_create on general principles.
-        AC_TRY_LINK([#include <pthread.h>],
+        AC_LINK_IFELSE([AC_LANG_PROGRAM([#include <pthread.h>],
                     [pthread_t th; pthread_join(th, 0);
                      pthread_attr_init(0); pthread_cleanup_push(0, 0);
-                     pthread_create(0,0,0,0); pthread_cleanup_pop(0); ],
-                    [acx_pthread_ok=yes])
+                     pthread_create(0,0,0,0); pthread_cleanup_pop(0)])],
+                    [acx_pthread_ok=yes],
+                    [])
 
         LIBS="$save_LIBS"
         CFLAGS="$save_CFLAGS"
@@ -225,8 +226,10 @@ if test "x$acx_pthread_ok" = xyes; then
 	AC_MSG_CHECKING([for joinable pthread attribute])
 	attr_name=unknown
 	for attr in PTHREAD_CREATE_JOINABLE PTHREAD_CREATE_UNDETACHED; do
-	    AC_TRY_LINK([#include <pthread.h>], [int attr=$attr; return attr;],
-                        [attr_name=$attr; break])
+            AC_LINK_IFELSE([AC_LANG_PROGRAM([#include <pthread.h>],
+                        [int attr=$attr; return attr])],
+                        [attr_name=$attr; break],
+                        [])
 	done
         AC_MSG_RESULT($attr_name)
         if test "$attr_name" != PTHREAD_CREATE_JOINABLE; then
